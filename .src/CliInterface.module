' Gambas module file

'Author  Westwood JustLostintime
'Released Under:
'  GNU GENERAL Public LICENSE
'  Version 3, 29 June 2007
'  See project license file.

Public AppDescription As String = ("This application emulates a 6502/65C02, its default is ctmon65 with tinybasic")
Public UpdateDevNoPrompt As Boolean = False

Public Sub main()

  Dim bVersion, bAbout, bDevMode, bPrintInstMatrix, bStartSingleStep, bVerbose, blistcomputers As Boolean
  Dim sComputerName As String

  Dim sDummy As String = Settings["Init", "Done"]

  System.IgnoreOverflow = True

  Settings["Init"] = sDummy
  FMain.ReadConfig()
  If Not Exist(FMain.AsmComputerDefinitions &/ FMain.CurrentComputerDefinition) Then
    FMain.CurrentComputerDefinition = "Default"
  Endif

  FMain.WriteConfig()

  Settings.Save()

  Args.Begin(Application.name & " <options>")
  bVersion = Args.Has("v", "version", ("Display the version Of the emulator"))
  bAbout = Args.Has("a", "about", ("Information about ") & Application.name)
  bDevMode = Args.Has("d", "devmode", ("Developer Mode, removes ~/6502 directory and recreates all default settings(be carefull all your stuff will be gone!). Usefull to get new application updates."))
  bPrintInstMatrix = Args.Has("p", "print", ("Print Supported Instruction Matrix and exit"))
  bStartSingleStep = Args.Has("s", "singlestep", ("Start the emulator in single step mode"))
  sComputerName = Args.Get("c", "computer", ("Provies the name of the computer to run, No GUI is started"), "", "")
  UpdateDevNoPrompt = Args.Has("u", "update", ("Update Device definitions no prompt"))
  bVerbose = Args.Has("V", "verbose", ("Display Verbose messages"))
  bListComputers = Args.Has("l", "list_computers", ("List all available computer definitions"))

  Args.End()

  '' Define the default assembled source and binary directories
  If bListComputers Then
    List_computer_info()
    Quit 0
  Endif

  With FMAIN

    If bVerbose Then .Verbose = True
    .ReadConfig()
    .WriteConfig()

  End With

  If bdevMode Then
    Try Shell.RmDir(FMain.AsmRootDirectory)
    Settings.Clear()
    Settings.Save()
  Endif

  CreateDefaults()

  If bVersion Then
    Print Application.name;; Application.Version
  Else If bAbout Then
    Print Application.name;; Application.Version; "\n"; AppDescription
  Else If bPrintInstMatrix Then
    PrintMatrix()
  Else If sComputerName <> "" Then
    RunComputer(sComputerName)
    If FMain.Verbose Then Error("End of Emulator session")                                                     'halt when we get here
  Else
    FMain.Show()
    While Not FMain.Closed
      Wait Next
    Wend
  Endif

  Quit 0

Catch
  Print "6502 Error "; Error.text;; "at";; Error.where
  Quit 1

End

Public Sub List_computer_info()

  Dim dirlist As String[] = Dir(User.home &/ "6502/computer", "", gb.file)
  Dim comp As ComputerDef

  Print "Available computers:"
  For Each s As String In dirlist
    comp = New ComputerDef("", s)
    Print "---------------------------------------------------------------------------------------------"
    If Left(Comp.Description, s.len) <> s Then Print comp.name
    Print comp.Description
    Print "Devices:"
    For Each si As String[] In comp
      Print "              "; Left(comp.key & Space(30), 30), Hex(si[0], 4), Hex(si[1], 4), Quote(si[2])
    Next
    Print
  Next

End


Public Sub PrintMatrix()

  With FMAIN
    .MyComputer = New Computer As "the_computer"
    Disassembler.InstMatrix()
  End With

End


Public Sub RunComputer(ComputerName As String)

  Dim Comp As ComputerDef

  With FMAIN
    .MyComputer = New Computer As "the_computer"
    Try Comp = New ComputerDef(.AsmComputerDefinitions, ComputerName)
    If Error Then
      Error Error.text
      Return
    Endif
    .MyComputer.Configure(Comp)
    Wait 0.001
    .MyComputer.StatusOff()
    .MyComputer.Trace(False)
    .MyComputer.UnsetSingleStep()
    .MyComputer.run()
  End With

End

Public Sub The_computer_Autostart(Address As Long, speed As Integer)

  With FMAIN
    If Address <> -1 Then .MyComputer.StartAddress = address
    .MyComputer.SpeedScaler = speed
  End With

End








