' Gambas module file

Public AppDescription As String = ("This application emulates a 6502, its default is ctmon65 with tinybasic")

Public Sub main()

  Dim bVersion, bAbout, bDevMode As Boolean
  Dim sComputerName As String

  Args.Begin(Application.name & " <options>")
  bVersion = Args.Has("v", "version", "Display the version Of the emulator")
  bAbout = Args.Has("a", "about", "Information about " & Application.name)
  bDevMode = Args.Has("d", "devmode", "Developer Mode, removes ~/6502 directory and recreats \nall default settings(be carefull all your stuff will be gone!)\n Usefull to get new application updates")
  sComputerName = Args.Get("c", "computer", "Provies the name of the computer to run, No GUI is started", "", "")
  Args.End()

  ''Define the default assembled source and binary directories
  With FMAIN
    .AsmSourceDirectory = Settings["AsmSourceDirectory", .AsmSourceDirectory]
    .AsmBinaryDirectory = Settings["AsmBinaryDirectory", .AsmBinaryDirectory]
    .AsmDeviceDirectory = Settings["AsmDeviceDirectory", .AsmDeviceDirectory]
    .AsmComputerDefinitions = Settings["AsmComputerDefinitions", .AsmComputerDefinitions]
    .AsmDrivesDirectory = Settings["AsmDrivesDirectory", .AsmDrivesDirectory]
    .AsmDocumentDirectory = Settings["AsmDocumentDirectory", .AsmDocumentDirectory]
    .CurrentComputerDefinition = Settings["computer/current", .DefaultComputerDefinition]
    .DefaultComputerDefinition = Settings["computer/default", .DefaultComputerDefinition]


    If bdevMode Then
      Try Shell.RmDir(.AsmRootDirectory)
      Settings.Clear()
    Endif
  End With

  CreateDefaults()

  If bVersion Then
    Print Application.name;; Application.Version
  Else If bAbout Then
    Print Application.name;; Application.Version; "\n"; AppDescription
  Else If sComputerName <> "" Then
    RunComputer(sComputerName)
  Else
    FMain.Show()
  Endif

  Quit 0

Catch
  Print "6502 Error "; Error.text;; "at";; Error.where
  Quit 1

End


Public Sub RunComputer(ComputerName As String)

  With FMAIN
    CreateDefaults()

    .MyComputer = New Computer As "the_computer"
    Dim Comp As New ComputerDef(.AsmComputerDefinitions, ComputerName)
    .MyComputer.Configure(Comp)
    Wait
    .MyComputer.StatusOff()
    .MyComputer.Trace(False)
    .MyComputer.UnsetSingleStep()
    .MyComputer.run()
  End With

End

Public Sub The_computer_Autostart(Address As Long, speed As Integer)

  With FMAIN
    .MyComputer.StartAddress = address
    .MyComputer.SpeedScaler = speed
  End With

End








